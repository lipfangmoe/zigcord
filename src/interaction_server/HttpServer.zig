//! Server which listens for Discord Interactions.
//! Only need to call `deinit()` if created via `init(Address)`.
//! If you have an existing `std.net.Address`, it is okay to create this struct via struct initialization.

const std = @import("std");
const zigcord = @import("../root.zig");
const Server = @This();
const InteractionRequest = @import("./InteractionRequest.zig");
const verify = @import("./verify.zig");

application_public_key: std.crypto.sign.Ed25519.PublicKey,
net_server: std.net.Server,

const application_public_key_bytes_len = std.crypto.sign.Ed25519.PublicKey.encoded_length;
const application_public_key_hex_len = application_public_key_bytes_len * 2; // requires 2 hex digits to represent 1 byte

pub fn init(address: std.net.Address, application_public_key_hex: [application_public_key_hex_len]u8) !Server {
    const net_server = try address.listen(.{});
    var application_public_key_bytes: [application_public_key_bytes_len]u8 = undefined;
    const slice = std.fmt.hexToBytes(&application_public_key_bytes, &application_public_key_hex) catch return error.InvalidApplicationKey;
    if (slice.len != application_public_key_bytes_len) {
        return error.InvalidApplicationKey;
    }
    return .{
        .net_server = net_server,
        .application_public_key = std.crypto.sign.Ed25519.PublicKey.fromBytes(application_public_key_bytes) catch return error.InvalidApplicationKey,
    };
}

/// Only need to call `deinit()` if created via `init(Address)`
pub fn deinit(self: *Server) void {
    self.net_server.deinit();
}

pub fn receiveInteraction(self: *Server, alloc: std.mem.Allocator) !InteractionRequest {
    while (true) {
        const conn = self.net_server.accept() catch |err| {
            zigcord.logger.warn("error occurred while accepting request: {}", .{err});
            continue;
        };
        var stream_writer_buf: [1000]u8 = undefined;
        var stream_writer = conn.stream.writer(&stream_writer_buf);

        var stream_reader_buf: [10000]u8 = undefined;
        var stream_reader = self.net_server.stream.reader(&stream_reader_buf);

        var http_server = std.http.Server.init(stream_reader.interface(), &stream_writer.interface);
        var http_req = http_server.receiveHead() catch |err| {
            zigcord.logger.warn("error occurred while receiving headers: {}", .{err});
            continue;
        };
        var sig_buf: [64]u8 = undefined;
        const signature_headers = verify.SignatureHeaders.initFromHttpRequest(&http_req, &sig_buf) catch |err| {
            zigcord.logger.warn("error occurred while looking for signature headers: {}", .{err});
            http_req.respond("", .{ .status = .unauthorized }) catch |respond_err| {
                zigcord.logger.warn("IO error occurred while writing error response: {}", .{respond_err});
            };
            continue;
        };

        var reader_buf: [1000]u8 = undefined;
        const body_reader = http_req.readerExpectContinue(&reader_buf) catch |err| {
            zigcord.logger.err("http error occurred while writing a response to 100-continue: {}", .{err});
            http_req.respond("", .{ .status = .expectation_failed }) catch |respond_err| {
                zigcord.logger.warn("IO error occurred while writing error response: {}", .{respond_err});
            };
            return error.HttpError;
        };
        const body = body_reader.allocRemaining(alloc, .limited(1024 * 1024)) catch |err| {
            zigcord.logger.err("error occurred while reading request body: {}", .{err});
            http_req.respond("", .{ .status = .internal_server_error }) catch |respond_err| {
                zigcord.logger.warn("IO error occurred while writing error response: {}", .{respond_err});
            };
            return error.BodyReadError;
        };

        verify.verify(signature_headers, body, self.application_public_key) catch |err| {
            switch (err) {
                error.InvalidPublicKey => {
                    zigcord.logger.err("Application Public Key is invalid: {}", .{self.application_public_key});
                    http_req.respond("", .{ .status = .internal_server_error }) catch |respond_err| {
                        zigcord.logger.warn("IO error occurred while writing error response: {}", .{respond_err});
                    };
                    return error.InvalidPublicKey;
                },
                error.SignatureVerificationError => {
                    zigcord.logger.warn("Signature verification failed, responding with 401", .{});
                    http_req.respond("", .{ .status = .unauthorized }) catch |respond_err| {
                        zigcord.logger.warn("IO error occurred while writing error response: {}", .{respond_err});
                    };
                    continue;
                },
            }
        };

        var req = InteractionRequest.init(alloc, body, http_req) catch |err| {
            zigcord.logger.err("error while parsing interaction: {}", .{err});
            return error.InteractionParseError;
        };
        if (req.interaction.type == .ping) {
            try req.respond(.initPong());
            continue;
        }

        return req;
    }
}
